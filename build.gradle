plugins {
	id 'java'
	id 'org.springframework.boot' version '3.2.3'
	id 'io.spring.dependency-management' version '1.1.4'
	id 'org.asciidoctor.jvm.convert' version '3.3.2'
	id 'com.epages.restdocs-api-spec' version '0.18.4'
	id 'org.hidetake.swagger.generator' version '2.18.2'
}

group = 'com.givemecon'
version = '0.0.1'

java {
	sourceCompatibility = '17'
}

repositories {
	mavenCentral()
}

configurations {
	asciidoctorExtensions
	compileOnly {
		extendsFrom annotationProcessor
	}
}

ext {
	set('snippetsDir', file("build/generated-snippets"))
}

asciidoctor {
	configurations 'asciidoctorExtensions'
	sourceDir 'src/docs/asciidoc'
	attributes 'snippets': file("build/generated-snippets")
}

dependencies {
	implementation 'org.springframework.boot:spring-boot-starter-web'
	implementation 'org.springframework.boot:spring-boot-starter-validation'
	implementation 'org.springframework.boot:spring-boot-starter-aop'
	implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
	implementation 'org.springframework.boot:spring-boot-starter-data-redis'
	implementation 'it.ozimov:embedded-redis:0.7.2'

	// Spring Security
	implementation 'org.springframework.boot:spring-boot-starter-security'
	testImplementation 'org.springframework.security:spring-security-test'

	// OAuth2
	implementation 'org.springframework.boot:spring-boot-starter-oauth2-client'

	// JWT
	implementation 'io.jsonwebtoken:jjwt-api:0.11.5'
	implementation 'io.jsonwebtoken:jjwt-impl:0.11.5'
	implementation 'io.jsonwebtoken:jjwt-jackson:0.11.5'

	// H2
	runtimeOnly 'com.h2database:h2'
	// MySQL
	runtimeOnly 'mysql:mysql-connector-java:8.0.33'

	// Spring Cloud (S3)
	implementation platform("io.awspring.cloud:spring-cloud-aws-dependencies:3.0.0")
	implementation 'io.awspring.cloud:spring-cloud-aws-starter-s3'
	implementation 'io.findify:s3mock_2.13:0.2.6'

	// Spring Cloud Google Cloud (For Cloud Vision API)
	implementation platform("com.google.cloud:spring-cloud-gcp-dependencies:4.1.3")
	implementation "com.google.cloud:spring-cloud-gcp-starter-vision"
	implementation "com.google.cloud:spring-cloud-gcp-starter-storage"

	// AWS Lambda
	implementation ('org.springframework.boot:spring-boot-starter-web') {
		exclude group: 'org.springframework.boot', module: 'spring-boot-starter-tomcat'
	}
	implementation 'com.amazonaws.serverless:aws-serverless-java-container-springboot3:2.0.1'

	// test
	testImplementation 'org.springframework.boot:spring-boot-starter-test'

	// API Docs
	testImplementation 'org.springframework.restdocs:spring-restdocs-mockmvc'
	asciidoctorExtensions 'org.springframework.restdocs:spring-restdocs-asciidoctor'
	testImplementation 'com.epages:restdocs-api-spec-mockmvc:0.18.4'
	implementation 'org.springdoc:springdoc-openapi-starter-webmvc-ui:2.0.2'

	// Lombok
	compileOnly 'org.projectlombok:lombok'
	annotationProcessor 'org.projectlombok:lombok'
	testCompileOnly 'org.projectlombok:lombok'
	testAnnotationProcessor 'org.projectlombok:lombok'
}

tasks.named('test') {
	outputs.dir snippetsDir
	useJUnitPlatform()
}

tasks.named('asciidoctor') {
	inputs.dir snippetsDir
	dependsOn test
}

openapi3 {
	servers = [
			{ url = 'http://localhost:8080' }
	]
	title = 'Post Service API'
	description = 'Post Service API description'
	version = '1.0.0'
	format = 'yml'
}

tasks.withType(GenerateSwaggerUI) {
	dependsOn 'openapi3'

	delete file('src/main/resources/static/docs/')
	copy {
		from "build/api-spec"
		into "src/main/resources/static/docs/"
	}
}

bootJar {
	dependsOn(':openapi3')
}